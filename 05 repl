(%require readline/readline)

;; http://arclanguage.org/item?id=10344
(let interactive (%:terminal-port? (current-input-port))
  (when interactive
    (%require readline/rep-start)
    ((% set-completion-function!)
      (fn (s)
        (collect:each (k _) (%.globals)
          (zap string k)
          (when (headmatch s k) ; TODO from lib/strings
            (yield k))))))

  (def repl ()
    ;; This causes Ctrl+C to return to the REPL, rather than aborting.
    ;; Technique was taken from Racket's (read-eval-print-loop) which
    ;; I found in /usr/share/racket/collects/racket/private/misc.ss
    (%.call-with-continuation-prompt
      (fn ()
        (on-err (fn (c)
                  ;; http://arclanguage.org/item?id=10344
                  (w/stdout (stderr)
                    (if debug?
                        ; Displays the stack trace when debug? is t
                        ((%.error-display-handler) (string "error: " details.c) c)
                        (prn "error: " details.c)))
                  ;; Abort to loop. (Calling `repl` directly would not be a tail call.)
                  (%:abort-current-continuation (default-continuation-prompt-tag)))
                (fn ()
                  (let expr (if interactive
                                ;; This is to make GNU readline work
                                (let it ((%.current-prompt-read))
                                  (if %.syntax?.it
                                      %.syntax->datum.it
                                      it))
                                (read (stdin) %.eof))
                        ;; this is to distinguish () from eof
                    (if (isnt expr %.eof)
                          (let that? (in expr 'that 'thatexpr)
                            (unless that?
                              (%.set-raw 'thatexpr expr))
                            (let val eval.expr
                              (unless that?
                                (%.set-raw 'that val))
                              (when interactive
                                (write val)
                                (prn))
                              ;; Abort to loop. (Calling `repl` directly would not be a tail call.)
                              (%:abort-current-continuation (default-continuation-prompt-tag))))
                        interactive
                          (prn))))))
      (%.default-continuation-prompt-tag)
      (fn args (repl))))
  (repl)
)
